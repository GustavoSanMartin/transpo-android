apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.squareup.sqldelight'
apply plugin: "androidx.navigation.safeargs.kotlin"

apply from: '../core_dependencies.gradle'
apply from: '../test_dependencies.gradle'
apply plugin: 'kotlin-android-extensions'

def props = new Properties()
file("../config.properties").withInputStream { props.load(it) }

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId "ca.llamabagel.transpo"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        sourceSets {
            androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "API_ENDPOINT", "\"${props.getProperty("api.endpoint")}\""
            buildConfigField "String", "MAPBOX_KEY", "\"${props.getProperty("mapbox.key")}\""
        }

        debug {
            applicationIdSuffix '.debug'
            buildConfigField "String", "API_ENDPOINT", "\"${props.getProperty("api.endpoint")}\""
            buildConfigField "String", "MAPBOX_KEY", "\"${props.getProperty("mapbox.key")}\""
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    dataBinding {
        enabled = true
    }
}

configurations {
    ktlint
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.0.0'
    ktlint "com.github.shyiko:ktlint:0.31.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

sqldelight {
    TransitDatabase {
        packageName = "ca.llamabagel.transpo.data.db"
    }
}
